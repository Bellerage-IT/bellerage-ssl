#Область ПрограммныйИнтерфейс


// Добавить пагинацию.
// 
// Параметры:
//  ТекстЗапроса - Строка
//  Порция - Число
//  Сдвиг - Число
//  ПакетЗапроса - Число - Номер пакета запроса
Процедура ДобавитьПагинацию(ТекстЗапроса, Порция, Сдвиг, ПакетЗапроса = Неопределено) Экспорт
	

	Если ТипЗнч(Порция) <> Тип("Число") Или ТипЗнч(Сдвиг) <> Тип("Число") Или Порция = 0 Тогда
		ВызватьИсключение "Недопустимые значения параметров: Порция и Сдвиг";
	КонецЕсли; 

	Схема = бф_МодельЗапроса.МодельЗапроса(ТекстЗапроса);

	Если ПакетЗапроса <> Неопределено Тогда
		Схема.УстановитьЗапросПакета(ПакетЗапроса);
	КонецЕсли;
	
	//@skip-check property-return-type, dynamic-access-method-not-found
	Если Схема.ЗапросПакета.Операторы.Количество() > 1 Тогда
		ВызватьИсключение "В пакете запроса несколько операторов выбрать, такой вариант не поддерживает";
	КонецЕсли;	
	
	Если Сдвиг = 0 Тогда
		Схема.Первые(Порция);
		ТекстЗапроса = Схема.ПолучитьТекстЗапроса();
		Возврат;
	КонецЕсли;
	
	ВспомогательнаяСхема = бф_МодельЗапроса.МодельЗапроса();
	ВспомогательнаяСхема.ЗапросПакета();
	//@skip-check invocation-parameter-type-intersect, property-return-type
	УсловиеНевхождения = Новый Массив();
	УсловиеНевхождения.Добавить("НЕ");
	УсловиеНевхождения.Добавить("(");
	ВыбираемыеПоля = Схема.ОператорВыбрать.ВыбираемыеПоля; // ПоляСхемыЗапроса
	ИменаВыбираемыхПолей = Новый Массив();
	Для Каждого Поле Из ВыбираемыеПоля Цикл
		ИменаВыбираемыхПолей.Добавить(Строка(Поле));
	КонецЦикла;
	УсловиеНевхождения.Добавить(СтрСоединить(ИменаВыбираемыхПолей, ","));
	УсловиеНевхождения.Добавить(")");
	УсловиеНевхождения.Добавить("В");
	УсловиеНевхождения.Добавить("(");
	УсловиеНевхождения.Добавить(СформироватьЗапросСдвига(Схема, Сдвиг));
	УсловиеНевхождения.Добавить(")");
	
	Схема.Отбор(СтрСоединить(УсловиеНевхождения, " "));
	
	ТекстЗапроса = Схема.Первые(Порция).ПолучитьТекстЗапроса();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьЗапросСдвига(Схема, Сдвиг) Экспорт
	
	СхемаЗапроса = Схема.СхемаЗапроса; // СхемаЗапроса
	
	КопияПакета = СхемаЗапроса.ПакетЗапросов.ДобавитьКопию(СхемаЗапроса.ПакетЗапросов.Индекс(Схема.ЗапросПакета));
	ВспомогательнаяМодель = бф_МодельЗапроса.МодельЗапроса(КопияПакета.ПолучитьТекстЗапроса());
	
	СхемаЗапроса.ПакетЗапросов.Удалить(СхемаЗапроса.ПакетЗапросов.Индекс(КопияПакета));
	
	Возврат ВспомогательнаяМодель.Первые(Сдвиг).ПолучитьТекстЗапроса();
	
КонецФункции

#КонецОбласти
