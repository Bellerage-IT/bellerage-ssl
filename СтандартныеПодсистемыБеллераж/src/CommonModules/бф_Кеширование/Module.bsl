// @strict-types

#Область ПрограммныйИнтерфейс
// Описание
// Возвращает значение из кеша по ключу
// Параметры:
//   НаборКлючей - Произвольный
//   			 - Массив из Произвольный
//   ЛокальныйКеш - см. ЛокальныйКеш
// Возвращаемое значение:
// Произвольный - Значение из кеша
Функция ЗначениеИзКеша(Знач НаборКлючей, ЛокальныйКеш) Экспорт
	
	Если ЛокальныйКеш = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(НаборКлючей) <> Тип("Массив") Тогда
		НаборКлючей = бф_ОбщегоНазначенияХТТП.ЗначениеВМассиве(НаборКлючей);
	КонецЕсли;
	
	Значение = ЛокальныйКеш.Получить(НаборКлючей[0]);
	Если Значение = Неопределено Тогда
		Возврат Неопределено;
	ИначеЕсли ТипЗнч(Значение) <> Тип("Соответствие") Тогда
		Возврат Значение;
	Иначе
		// Рекурсивно копируем массив и передаем в функцию дальше
		// Не используем функцию ОбщегоНазначения, т.к. она достаточно долгая, здесь ситуация проще и вложенное копирование не требуется
		НовыйНабор = Новый Массив(Новый ФиксированныйМассив(НаборКлючей));
		НовыйНабор.Удалить(0);
		Возврат ЗначениеИзКеша(НовыйНабор, Значение);
	КонецЕсли;
	
КонецФункции

// Описание
// Возвращает первичное соответствие по ключу показателя
// Параметры:
//   КешПараметров - Структура - Общий кеш
//   ИмяСвойства - Строка - Имя свойства локального кеша
// Возвращаемое значение:
// Соответствие из КлючИЗначение
Функция ЛокальныйКеш(КешПараметров, ИмяСвойства) Экспорт
	
	Если КешПараметров = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не КешПараметров.Свойство(ИмяСвойства) Тогда
		КешПараметров.Вставить(ИмяСвойства, Новый Соответствие());
	КонецЕсли;
	Возврат КешПараметров[ИмяСвойства];
КонецФункции

// Описание
// Помещает значени в кеш по набору ключей рекурсивно
// Порядок ключей определяется порядком в наборе
// ВНИМАНИЕ 1! Значение не должно быть типом Соответствие, используйте ФиксированноеСоответствие
// ВНИМАНИЕ 2! Значение Неопределено не поддерживается для кеширования
// Параметры:
//   Значение - Произвольный
//   НаборКлючей - Массив из Произвольный
//   			 - Произвольный
//   ЛокальныйКеш - см. ЛокальныйКеш
Процедура ПоместитьЗначениеВКеш(Значение, Знач НаборКлючей, ЛокальныйКеш) Экспорт    
	
	Если ЛокальныйКеш = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если ТипЗнч(НаборКлючей) <> Тип("Массив") Тогда
		НаборКлючей = бф_ОбщегоНазначенияХТТП.ЗначениеВМассиве(НаборКлючей);
	КонецЕсли;
	
	ТекСоответствие = ЛокальныйКеш.Получить(НаборКлючей[0]);
	Если ТекСоответствие = Неопределено Тогда
		Если НаборКлючей.Количество() = 1 Тогда
			ЛокальныйКеш.Вставить(НаборКлючей[0], Значение);
			Возврат;
		Иначе
			ЛокальныйКеш.Вставить(НаборКлючей[0], Новый Соответствие);
		КонецЕсли;
	КонецЕсли;
	
	НовыйНабор = бф_ОбщегоНазначенияХТТП.СкопироватьРекурсивно(НаборКлючей);
	НовыйНабор.Удалить(0);
	ПоместитьЗначениеВКеш(Значение, НовыйНабор, ЛокальныйКеш.Получить(НаборКлючей[0]));
	
КонецПроцедуры

#КонецОбласти